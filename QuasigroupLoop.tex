\chapter{Theory Of Quasigroup and Loop in Agda}
Applications of non-associative algebras are explored in various fields of
study. For example, Einstein's formula of addition of velocities gives a loop
structure \cite{ungar2007einstein}. Quasigroups of various orders are used in
field of cryptography \cite{phillips2010automated}. Lie algebra is used in
differential geometry\cite{quasigroupWiki}. With proof assistants, such as Agda,
we can verify the relevant mathematical proofs of these algebraic structures.
They are interactive software that help to derive complex mathematical proofs.
In this chapter, we formalize two important non-associative algebras -
quasigroup, and loop structure. A \textit{quasigroup} (Q, ∙, /, \textbackslash)
is a type (2,2,2) algebra for which the binary operations \textbackslash (left
division) and / (right division) are defined such that division is always
possible.  A \textit{loop} is a quasigroup with identity. In this chapter, we
explore morphisms and direct product for these structures and derive proofs for
some of the properties of these structures.
\section{Definitions}
A set that has a binary operation is called Magma. In this case a Magma is total
and should not be confused with groupoid that need not be total. Formally, A
magma is a set $S$ with a binary operation $∙$ such that, $\forall x,y \in S
\Rightarrow (x ∙ y) \in S$. In Agda, magma structure is defined as
\inline{IsMagma} with binary operation $∙$. A quasigroup can be defined as a
magma with left and right division identities. In other words, a quasigroup is a
set with a binary operation that satisfies the property that for every element
in the set, there is a unique element in the set that provides a solution to the
equation. The operation $\backslash$ (left division) and $/$ (right division)
for elements $x,y$ in a quasigroup is defined as:

\begin{equation} \label{eq_L-leftdivides}
y\ =\ x\ ∙\ (x\ \backslash\ y)
\end{equation}
\begin{equation} \label{eq_L-rightdivides}
y\ =\ x\ \backslash\ (x\ ∙\ y)
\end{equation}
\begin{equation} \label{eq_R-leftdivides}
y\ =\ (y\ /\ x)\ ∙\ x
\end{equation}
\begin{equation} \label{eq_Rirightdivides}
y\ =\ (y\ ∙\ x)\ /\ x
\end{equation}
Conversely, in Agda, we may write this as:
\begin{minted}[samepage]{Agda}
LeftDividesˡ : Op₂ A → Op₂ A → Set _
LeftDividesˡ _∙_  _\\_ = ∀ x y → (x ∙ (x \\ y)) ≈ y
\end{minted}
\begin{minted}[samepage]{Agda}
LeftDividesʳ : Op₂ A → Op₂ A → Set _
LeftDividesʳ _∙_ _\\_ = ∀ x y → (x \\ (x ∙ y)) ≈ y
\end{minted}
\begin{minted}[samepage]{Agda}
RightDividesˡ : Op₂ A → Op₂ A → Set _
RightDividesˡ _∙_ _//_ = ∀ x y → ((y // x) ∙ x) ≈ y
\end{minted}
\begin{minted}[samepage]{Agda}
RightDividesʳ : Op₂ A → Op₂ A → Set _
RightDividesʳ _∙_ _//_ = ∀ x y → ((y ∙ x) // x) ≈ y
\end{minted}

Afterwards, we can form left and right divisions as:

\begin{minted}[samepage]{Agda}
LeftDivides : Op₂ A → Op₂ A → Set _
LeftDivides ∙ \\ = (LeftDividesˡ ∙ \\) × (LeftDividesʳ ∙ \\)

RightDivides : Op₂ A → Op₂ A → Set _
RightDivides ∙ // = (RightDividesˡ ∙ //) × (RightDividesʳ ∙ //)
\end{minted}

Note that we use // and \textbackslash\textbackslash instead of / and
\textbackslash \ respectively to overcome the conflict with overloaded or escape
characters. 

The Quasigroup structure can be structurally derived from Magma in Agda as:

\begin{minted}[samepage]{Agda}
record IsQuasigroup (∙ \\ // : Op₂ A) : Set (a ⊔ ℓ) where
field
  isMagma       : IsMagma ∙
  \\-cong       : Congruent₂ \\
  //-cong       : Congruent₂ //
  leftDivides   : LeftDivides ∙ \\
  rightDivides  : RightDivides ∙ //

open IsMagma isMagma public
\end{minted}
In the above definition of \inline{IsQuasigroup} is a record type with three
binary operations $∙,\ \backslash\backslash \ //$ on some set $A$. (a ⊔ ℓ)
returns the largest of two \inline{Level} \footnote{Level are used in universe
polymorphism discussed in Chapter 3} (a,ℓ). The structure has five fields.
\inline{isMagma} field is used to say that the structure \inline{IsQuasigroup}
has a structure \inline{IsMagma} with other following predicates.
\inline{\\-cong} and \inline{//-cong} field are used to say that the division
operations are congruent. \inline{Congruent₂ <//,\\>} is used to say that the
binary division operation is congruent.  The division predicates are given using
\inline{leftDivides} and \inline{rightDivides} from the definition
\inline{LeftDivides} and \inline{RightDivides} above. We then oepn
\inline{IsMagma} as public to bring its definitions into scope.

A loop is a quasigroup that has identity element. The identity axiom is given
as:
\begin{equation}\label{eq_lineslope}
x\ ∙\ e\ =\ e\ ∙\ x\ =\ x
\end{equation}
Conversely, in Agda, (left-right) identity is defined as:
\begin{minted}[samepage]{Agda}
LeftIdentity : A → Op₂ A → Set _
LeftIdentity e _∙_ = ∀ x → (e ∙ x) ≈ x

RightIdentity : A → Op₂ A → Set _
RightIdentity e _∙_ = ∀ x → (x ∙ e) ≈ x

Identity : A → Op₂ A → Set _
Identity e ∙ = (LeftIdentity e ∙) × (RightIdentity e ∙)
\end{minted}

Similar to quasigroup, loop structure can be structurally derived from
quasigroup.

\begin{minted}[samepage]{Agda}
record IsLoop (∙ \\ // : Op₂ A) (ε : A) : Set (a ⊔ ℓ) where
field
  isQuasigroup : IsQuasigroup ∙ \\ //
  identity     : Identity ε ∙

open IsQuasigroup isQuasigroup public
\end{minted}
A loop is called a \textit{right bol loop} if it satisfies the identity
(Equation~\ref{eq_rightbol})
\begin{equation}\label{eq_rightbol}
 ((z\ ∙\ x)\ ∙\ y)\ ∙\ x\ =\ z\ ∙\ ((\ x\ ∙\ y)\ ∙\ x)
\end{equation}
A loop is called a \textit{left bol loop} if it satisfies the identity
(Equation~\ref{eq_leftbol})
\begin{equation}\label{eq_leftbol}
 x\ ∙\ (y\ ∙\ (x\ ∙\ z))\ =\ (x\ ∙\ (y\ ∙\ x))\ ∙\ z
\end{equation}
A loop is called \textit{middle bol loop} if it satisfies the identity
(Equation~\ref{eq_middlebol}) 
\begin{equation}\label{eq_middlebol}
(z\ ∙\ x)\ ∙\ (y\ ∙\ z)\ =\ z\ ∙\ ((x\ ∙\ y)\ ∙\ z)
\end{equation}
A left-right bol loop is called a \textit{moufang loop} if it satisfies identity
(Equation~\ref{eq_moufang})
\begin{equation}\label{eq_moufang}
(z\ ∙\ x)\ ∙\ (y\ ∙\ z)\ =\ z\ ∙\ ((x\ ∙\ y)\ ∙\ z)
\end{equation} 

\begin{minted}[breaklines,samepage]{Agda}
LeftBol : Op₂ A → Set _
LeftBol _∙_ = ∀ x y z → (x ∙ (y ∙ (x ∙ z))) ≈ ((x ∙ (y ∙ x)) ∙ z )
\end{minted}
\begin{minted}[breaklines,samepage]{Agda}
RightBol : Op₂ A → Set _
RightBol _∙_ = ∀ x y z → (((z ∙ x) ∙ y) ∙ x) ≈ (z ∙ ((x ∙ y) ∙ x))
\end{minted}
\begin{minted}[breaklines,samepage]{Agda}
MiddleBol : Op₂ A → Op₂ A  → Op₂ A  → Set _
MiddleBol _∙_ _\\_ _//_ = ∀ x y z → (x ∙ ((y ∙ z) \\ x)) ≈ ((x // z) ∙ (y \\ x))
\end{minted}
\begin{minted}[samepage]{Agda}
Identical : Op₂ A → Set _
Identical _∙_ = ∀ x y z → ((z ∙ x) ∙ (y ∙ z)) ≈ (z ∙ ((x ∙ y) ∙ z))
\end{minted}

\section{Morphism}
A structure preserving map f between two structures of same type is called
\textit{morphism} or homomorphism in general. That is \(f\) : \(A \ \rightarrow \
B\) and ∙ is an operation on the structure then homomorphism is defined as
\[f(x\  ∙ \  y) \ = \ f(x) \ ∙ \  f(y)\] A homomorphism that is injective is
called \textit{monomorphism}. If the structures are identical i.e., they are
more than just similar in structure then we can compare the structures with
isomorphism. A homomorphism that is bijective is called \textit{isomorphism}.
The quasigroup homomorphism preserves both left and right division
operations. Morphisms are important in understanding the relationships between
different quasigroups and loops and can be used to prove important theorems
about these structures.

For quasigroups $(Q_1,∙,\backslash \backslash ,//)$ and $(Q_2,\circ,\backslash ,/)$,
homomorphism is defined as a structure preserving map \( f:(Q_1,∙,\backslash
\backslash,//) \rightarrow (Q_2,\circ,\backslash,/) \) such that:
\begin{itemize}
  \item $f$ preserves the binary operation: $f(x∙y) = f(x) \circ f(y)$
  \item $f$ preserves the left division operation : $f(x\backslash \backslash y) = f(x)\backslash f(y)$
  \item $f$ preserves the right division operation: $f(x//y) = f(x)/f(y)$
\end{itemize}
In Agda, quasigroup homomorphism can be defined as:
\begin{minted}[samepage]{Agda}
record IsQuasigroupHomomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isRelHomomorphism : IsRelHomomorphism _≈₁_ _≈₂_ ⟦_⟧
    ∙-homo            : Homomorphic₂ ⟦_⟧ _∙₁_ _∙₂_
    \\-homo           : Homomorphic₂ ⟦_⟧ _\\₁_ _\\₂_
    //-homo           : Homomorphic₂ ⟦_⟧ _//₁_ _//₂_

  open IsRelHomomorphism isRelHomomorphism public
    renaming (cong to ⟦⟧-cong)
\end{minted}
In the above definition of quasigroup homomorphism, \inline{Homomorphic₂} is the
structure preserving map on some set A and B with binary operations ∙ and
$\circ$ respectively.
\begin{minted}[samepage]{Agda}
Homomorphic₂ : (A → B) → Op₂ A → Op₂ B → Set _
Homomorphic₂ ⟦_⟧ _∙_ _∘_ = ∀ x y → ⟦ x ∙ y ⟧ ≈ (⟦ x ⟧ ∘ ⟦ y ⟧)
\end{minted}

In the code above, \inline{⟦_⟧} is the map $(A \rightarrow B)$ that takes one
argument. Similar to quasigroup homomorphism, quasigroup monomorphism and
isomorphism can be defined as: 

\begin{minted}[samepage]{Agda}
record IsQuasigroupMonomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isQuasigroupHomomorphism : IsQuasigroupHomomorphism ⟦_⟧
    injective                : Injective ⟦_⟧

  open IsQuasigroupHomomorphism isQuasigroupHomomorphism public
\end{minted}
\begin{minted}[samepage]{Agda}
record IsQuasigroupIsomorphism (⟦_⟧ : A → B) : Set (a ⊔ b ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isQuasigroupMonomorphism : IsQuasigroupMonomorphism ⟦_⟧
    surjective               : Surjective ⟦_⟧

  open IsQuasigroupMonomorphism isQuasigroupMonomorphism public
\end{minted}

The loop homomorphism preserves left and right divisions along with the identity
element. The homomorphism $f$ preserves all the binary operations as quasigroup
along with the identity element. That is if \( f:(L_1,∙,\backslash
\backslash,//,e_1) \rightarrow (L_2,\circ,\backslash,/,e_2) \) is a loop homomorphism if
it is a quasigroup homomorphism such that: 
\[f(e_1) = e_2\] where $e_1$ is the identity element of loop $L_1$ and $e_2$ is
the identity element of loop $L_2$
In Agda, loop homomorphism can be defined using quasigroup homomorphism as:
\begin{minted}[breaklines,samepage]{Agda}
record IsLoopHomomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isQuasigroupHomomorphism : IsQuasigroupHomomorphism ⟦_⟧
    ε-homo                   : Homomorphic₀ ⟦_⟧ ε₁ ε₂

  open IsQuasigroupHomomorphism isQuasigroupHomomorphism public
\end{minted}
In the loop homomorphism defined above, \inline{Homomorphic₀} is a structure
preserving map for a nullary element and is defined as:
\begin{minted}[samepage]{Agda}
Homomorphic₀ : (A → B) → A → B → Set _
Homomorphic₀ ⟦_⟧ ∙ ∘ = ⟦ ∙ ⟧ ≈ ∘
\end{minted}
Similarly, loop monomorphism and loop isomorphism are defined in Agda as:
\begin{minted}[samepage]{Agda}
record IsLoopMonomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isLoopHomomorphism   : IsLoopHomomorphism ⟦_⟧
    injective            : Injective ⟦_⟧

  open IsLoopHomomorphism isLoopHomomorphism public
\end{minted}
\begin{minted}[samepage]{Agda}  
record IsLoopIsomorphism (⟦_⟧ : A → B) : Set (a ⊔ b ⊔ ℓ₁ ⊔ ℓ₂) where
  field
    isLoopMonomorphism   : IsLoopMonomorphism ⟦_⟧
    surjective           : Surjective ⟦_⟧

  open IsLoopMonomorphism isLoopMonomorphism public
\end{minted}
\section{Morphism composition}
If $f$ is a morphism such that $f\ :\ a \ \rightarrow \ b$ and $g$ is a morphism on same
structure such that $g\ :\ b\ \rightarrow \ c$, then composition of morphism can be
defined as $g \ ∘\ f\ :\ a \ \rightarrow \ c$. 
\begin{minted}[breaklines,samepage]{Agda}
isQuasigroupHomomorphism
  : IsQuasigroupHomomorphism Q₁ Q₂ f
  → IsQuasigroupHomomorphism Q₂ Q₃ g
  → IsQuasigroupHomomorphism Q₁ Q₃ (g ∘ f)
isQuasigroupHomomorphism f-homo g-homo = record
  { isRelHomomorphism = isRelHomomorphism F.isRelHomomorphism G.isRelHomomorphism
  ; ∙-homo              = λ x y → ≈₃-trans (G.⟦⟧-cong ( F.∙-homo x y )) ( G.∙-homo (f x) (f y) )
  ; \\-homo              = λ x y → ≈₃-trans (G.⟦⟧-cong ( F.\\-homo x y )) ( G.\\-homo (f x) (f y) )
  ; //-homo              = λ x y → ≈₃-trans (G.⟦⟧-cong ( F.//-homo x y )) ( G.//-homo (f x) (f y) )
  } where module F = IsQuasigroupHomomorphism f-homo; 
          module G = IsQuasigroupHomomorphism g-homo
\end{minted}
In the above quasigroup homomorphism composition, \inline{f} is a homomorphism
from quasigroup Q₁ to Q₂, \inline{g} is a homomorphism from quasigroup Q₂ to Q₃.
\inline{isRelHomomorphism} field givese the composition of homomorphism for a
homogeneous binary relation (≈). We can prove that the composition for binary
operations homomorphism (∙) for quasigroup is homomorphic using transitive
relation \inline{≈₃-trans} such that \[g (f ((Q₁ ∙ x) y)) ≈ (g ((Q₂ ∙ f x) (f
y)) \text{ and } g ((Q₂ ∙ f x) (f y))) ≈ ((Q₃ ∙ g (f x)) (g (f y)))\]
\[\Rightarrow g (f ((Q₁ ∙ x) y)) ≈ ((Q₃ ∙ g (f x)) (g (f y)))\]

Similarly, composition of loop homomorphism is defined as:
\begin{minted}[breaklines,samepage]{Agda}
isLoopHomomorphism
  : IsLoopHomomorphism L₁ L₂ f
  → IsLoopHomomorphism L₂ L₃ g
  → IsLoopHomomorphism L₁ L₃ (g ∘ f)
isLoopHomomorphism f-homo g-homo = record
  { isQuasigroupHomomorphism = isQuasigroupHomomorphism ≈₃-trans F.isQuasigroupHomomorphism G.isQuasigroupHomomorphism
  ; ε-homo = ≈₃-trans (G.⟦⟧-cong F.ε-homo) G.ε-homo
  } where module F = IsLoopHomomorphism f-homo; 
          module G = IsLoopHomomorphism g-homo
\end{minted}

Monomorphism and isomorphism compositions constructs for quasigroup and loop are
defined similar to homomorphism and can be found in Agda standard library.

\section{Direct Product}
The \textit{direct product} $M \ \times \ N$ of two quasigroups $M$ and $N$ is
defined as a pair $(m,n)$ where $m \ \in \ M$ and $n \ \in \ N$. The direct
product construct of left (right/middle) bol loop and moufang loop can be found
in Agda standard library.
\begin{minted}[breaklines,samepage]{Agda}
quasigroup : Quasigroup a ℓ₁ → Quasigroup b ℓ₂ → Quasigroup (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
quasigroup M N = record
  { _\\_    = zip M._\\_ N._\\_
  ; _//_    = zip M._//_ N._//_
  ; isQuasigroup = record
    { isMagma = Magma.isMagma (magma M.magma N.magma)
    ; \\-cong = zip M.\\-cong N.\\-cong
    ; //-cong = zip M.//-cong N.//-cong
    ; leftDivides = (λ x y → M.leftDividesˡ , N.leftDividesˡ <*> x <*> y) , (λ x y → M.leftDividesʳ , N.leftDividesʳ <*> x <*> y)
    ; rightDivides = (λ x y → M.rightDividesˡ , N.rightDividesˡ <*> x <*> y) , (λ x y → M.rightDividesʳ , N.rightDividesʳ <*> x <*> y)
    }
  } where module M = Quasigroup M; module N = Quasigroup N
\end{minted}
In the above code, \inline{zip} gives a $\Sigma$-type of dependent pairs.
\inline{<*>} is used to convert the curried functions to a function on pair.
Currying a function is to break down a function that takes multiple arguments
into a series of function that takes exactly one argument. The direct product of
loop structure can be defined similar to quasigroup as:
\begin{minted}[breaklines,samepage]{Agda}
loop : Loop a ℓ₁ → Loop b ℓ₂ → Loop (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
loop M N = record
  { ε = M.ε , N.ε
  ; isLoop = record
    { isQuasigroup = Quasigroup.isQuasigroup (quasigroup M.quasigroup N.quasigroup)
    ; identity = (M.identityˡ , N.identityˡ <*>_)
                , (M.identityʳ , N.identityʳ <*>_)
    }
  } where module M = Loop M; module N = Loop N
\end{minted}

\section{Properties}
In this section we prove some properties of quasigroup, loop, middle bol loop, and moufang loop using
Agda.
\subsection{Properties of Quasigroup}
Let $(Q, ∙, /, \backslash)$ be a quasigroup then:
\begin{enumerate}
\item Q is cancellative. A quasigroup is left cancellative if x ∙ y = x ∙ z then
y = z and a quasigroup is right cancellative if y ∙ x = z ∙ x then y = z. A
quasigroup is cancellative if it is both left and right cancellative.
\item \(\forall \ x,\ y,\ z\ \in Q \ ,\text{If} \ x\ ∙\ y\ =\ z\ \text{then}\ y\ =\ x\ \backslash \ z\)
\item \(\forall \ x,\ y,\ z\ \in \ Q \ ,\text{If} \ x\ ∙\ y\ =\ z\ \text{then}\ x\ = z\ /\ y\)
\end{enumerate}
Proof:
\begin{enumerate}
\item 
\begin{minted}[breaklines,samepage]{Agda}
cancelˡ : LeftCancellative _∙_
cancelˡ x y z eq = begin
  y             ≈⟨ sym( leftDividesʳ x y) ⟩
  x \\ (x ∙ y)  ≈⟨ \\-congˡ eq ⟩
  x \\ (x ∙ z)  ≈⟨ leftDividesʳ x z ⟩
  z             ∎

cancelʳ : RightCancellative _∙_
cancelʳ x y z eq = begin
  y             ≈⟨ sym( rightDividesʳ x y) ⟩
  (y ∙ x) // x  ≈⟨ //-congʳ eq ⟩
  (z ∙ x) // x  ≈⟨ rightDividesʳ x z ⟩
  z             ∎

cancel : Cancellative _∙_
cancel = cancelˡ , cancelʳ
\end{minted}

\item 
\begin{minted}[breaklines,samepage]{Agda}
y≈x\\z : ∀ x y z → x ∙ y ≈ z → y ≈ x \\ z
y≈x\\z x y z eq = begin
  y            ≈⟨ sym (leftDividesʳ x y) ⟩
  x \\ (x ∙ y) ≈⟨ \\-congˡ eq ⟩
  x \\ z       ∎
\end{minted}

\item 
\begin{minted}[breaklines,samepage]{Agda}
x≈z//y : ∀ x y z → x ∙ y ≈ z → x ≈ z // y
x≈z//y x y z eq = begin
  x            ≈⟨ sym (rightDividesʳ y x) ⟩
  (x ∙ y) // y ≈⟨ //-congʳ eq ⟩
  z // y       ∎
\end{minted}
\end{enumerate}

\subsection{Properties of Loop}
Properties of division operation holds for a loop.\\
Let $(L, ∙, /, \backslash)$ be a Loop with identity $x\ ∙\ e\ =\ x$ then the following properties holds 
\begin{enumerate}
\item \(\forall\ x\ \in\ L, \ x \ /\  x\ =\ e\) 
\item \(\forall \ x\ \in\ L,\  x\ \backslash \ x\ =\ e\)
\item \(\forall \ x\ \in\ L,\ e\ \backslash \ x\ =\ x\) 
\item \(\forall \ x\ \in\ L, \ x\ /\ e\ =\ x\) 
\end{enumerate}
Proof:
\begin{enumerate}
\item 
\begin{minted}[breaklines,samepage]{Agda}
x//x≈ε : ∀ x → x // x ≈ ε
x//x≈ε x = begin
  x // x       ≈⟨ //-congʳ (sym (identityˡ x)) ⟩
  (ε ∙ x) // x ≈⟨ rightDividesʳ x ε ⟩
  ε            ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x\\x≈ε : ∀ x → x \\ x ≈ ε
x\\x≈ε x = begin
  x \\ x       ≈⟨ \\-congˡ (sym (identityʳ x )) ⟩
  x \\ (x ∙ ε) ≈⟨ leftDividesʳ x ε ⟩
  ε            ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
ε\\x≈x : ∀ x → ε \\ x ≈ x
ε\\x≈x x = begin
  ε \\ x       ≈⟨ sym (identityˡ (ε \\ x)) ⟩
  ε ∙ (ε \\ x) ≈⟨ leftDividesˡ ε x ⟩
  x            ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x//ε≈x : ∀ x → x // ε ≈ x
x//ε≈x x = begin
  x // ε       ≈⟨ sym (identityʳ (x // ε)) ⟩
  (x // ε) ∙ ε ≈⟨ rightDividesˡ ε x ⟩
  x            ∎
\end{minted}
\end{enumerate}
\subsection{Properties of Middle bol loop}
Let $(M, ∙, /, \backslash)$ be a middle bol loop then the following
identities holds.
\begin{enumerate}
\item \(\forall\ x\ y\ z\ \in\ M,\ x\ ∙\ ((y\ ∙\ x)\ \backslash \ x)\ =\ y\ \backslash\ x\) 
\item \(\forall\ x\ y\ z\ \in\ M,\ x\ ∙\ ((x\ ∙\ z)\ \backslash \ x)\ =\ x\ /\ z\)
\item \(\forall\ x\ y\ z\ \in\ M,\ x\ ∙ (z\ \backslash\ x)\ =\ (x\ /\ z)\ ∙\ x\)
\item \(\forall\ x\ y\ z\ \in\ M,\ (x\ /\ (y\ ∙\ z))\ ∙\ x\ =\ (x\ /\ z)\ ∙\ (y\ \backslash\ x)\)
\item \(\forall\ x\ y\ z\ \in\ M,\ (x\ /\ (y\ ∙\ x))\ ∙\ x\ =\ y\ \backslash \ x\)
\item \(\forall\ x\ y\ z\ \in\ M,\ (x\ /\ (x\ ∙\ z))\ ∙\ x\ = \ x\ /\  z\)
\end{enumerate}
Proof:
\begin{enumerate}
\item
\begin{minted}[breaklines,samepage]{Agda}
xyx\\x≈y\\x : ∀ x y → x ∙ ((y ∙ x) \\ x) ≈ y \\ x
xyx\\x≈y\\x x y = begin
  x ∙ ((y ∙ x) \\ x)  ≈⟨ middleBol x y x ⟩
  (x // x) ∙ (y \\ x) ≈⟨ ∙-congʳ (x//x≈ε x) ⟩
  ε ∙ (y \\ x)        ≈⟨ identityˡ ((y \\ x)) ⟩
  y \\ x              ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
xxz\\x≈x//z : ∀ x z → x ∙ ((x ∙ z) \\ x) ≈ x // z
xxz\\x≈x//z x z = begin
  x ∙ ((x ∙ z) \\ x)  ≈⟨ middleBol x x z ⟩
  (x // z) ∙ (x \\ x) ≈⟨ ∙-congˡ (x\\x≈ε x) ⟩
  (x // z) ∙ ε        ≈⟨ identityʳ ((x // z)) ⟩
  x // z              ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
xz\\x≈x//zx : ∀ x z → x ∙ (z \\ x) ≈ (x // z) ∙ x
xz\\x≈x//zx x z = begin
  x ∙ (z \\ x)       ≈⟨ ∙-congˡ (\\-congʳ( sym (identityˡ z))) ⟩
  x ∙ ((ε ∙ z) \\ x) ≈⟨ middleBol x ε z ⟩
  x // z ∙ (ε \\ x)  ≈⟨ ∙-congˡ (ε\\x≈x x) ⟩
  x // z ∙ x         ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x//yzx≈x//zy\\x : ∀ x y z → (x // (y ∙ z)) ∙ x ≈ (x // z) ∙ (y \\ x)
x//yzx≈x//zy\\x x y z = begin
  (x // (y ∙ z)) ∙ x  ≈⟨ sym (xz\\x≈x//zx x ((y ∙ z))) ⟩
  x ∙ ((y ∙ z) \\ x)  ≈⟨ middleBol x y z ⟩
  (x // z) ∙ (y \\ x) ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x//yxx≈y\\x : ∀ x y → (x // (y ∙ x)) ∙ x ≈ y \\ x
x//yxx≈y\\x x y = begin
  (x // (y ∙ x)) ∙ x  ≈⟨ x//yzx≈x//zy\\x  x y x ⟩
  (x // x) ∙ (y \\ x) ≈⟨ ∙-congʳ (x//x≈ε x) ⟩
  ε ∙ (y \\ x)        ≈⟨ identityˡ ((y \\ x)) ⟩
  y \\ x              ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x//xzx≈x//z : ∀ x z → (x // (x ∙ z)) ∙ x ≈ x // z
x//xzx≈x//z x z = begin
  (x // (x ∙ z)) ∙ x  ≈⟨ x//yzx≈x//zy\\x x x z ⟩
  (x // z) ∙ (x \\ x) ≈⟨ ∙-congˡ (x\\x≈ε x) ⟩
  (x // z) ∙ ε        ≈⟨ identityʳ (x // z) ⟩
  x // z              ∎    
\end{minted}
\end{enumerate}
\subsection{Properties of Moufang Loop}
Let $(M, ∙, /, \backslash)$ be a moufang loop then the following identities
holds.
\begin{enumerate}
\item Moufang loop is alternative. A moufang loop is left alternative if it
satisfies\\ $(x\ ∙\ x)\ ∙\ y\ =\ x\ ∙\ (x\ ∙\ y)$, a moufang loop is right
alternative if it satisfies $x\ ∙\ (y\ ∙\ y)\ =\ (x\ ∙ y)\ ∙\ y$ and if a moufang loop
alternative if it is both left and right alternative. 
\item Moufang loop is flexible. A Moufang loop is flexible if it satisfies
flexible identity $(x\ ∙\ y)\ ∙\ x\ =\ x\ ∙\ (y\ ∙\ x)$
\item $\forall\ x\ y\ z\ \in\ M,\ z\ ∙\ (x\ ∙\ (z\ ∙\ y))\ =\ ((z\ ∙\ x)\ ∙\ z)\ ∙\ y$
\item $\forall\ x\ y\ z\ \in\ M,\ x\ ∙\ (z\ ∙\ (y\ ∙\ z))\ =\ ((x\ ∙\ z)\ ∙\ y)\ ∙\ z$ 
\item $\forall\ x\ y\ z\ \in\ M,\ z\ ∙\ ((x\ ∙\ y)\ ∙\ z)\ =\ (z\ ∙ \ (x\ ∙\ y))\ ∙\ z$ 
\end{enumerate}
Proof:
\begin{enumerate}
\item
\begin{minted}[breaklines,samepage]{Agda}
alternativeˡ : LeftAlternative _∙_
alternativeˡ x y = begin
  (x ∙ x) ∙ y       ≈⟨ ∙-congʳ (∙-congˡ (sym (identityˡ x))) ⟩
  (x ∙ (ε ∙ x)) ∙ y ≈⟨ sym (leftBol x ε y) ⟩
  x ∙ (ε ∙ (x ∙ y)) ≈⟨ ∙-congˡ (identityˡ ((x ∙ y))) ⟩
  x ∙ (x ∙ y)       ∎

alternativeʳ : RightAlternative _∙_
alternativeʳ x y = begin
  x ∙ (y ∙ y)         ≈⟨ ∙-congˡ(∙-congʳ(sym (identityʳ y))) ⟩
  x ∙ ((y ∙ ε) ∙ y)   ≈⟨ sym (rightBol y ε x) ⟩
  ((x ∙ y) ∙ ε ) ∙ y  ≈⟨ ∙-congʳ (identityʳ ((x ∙ y))) ⟩
  (x ∙ y) ∙ y         ∎

alternative : Alternative _∙_
alternative = alternativeˡ , alternativeʳ
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
flex : Flexible _∙_
flex x y = begin
  (x ∙ y) ∙ x       ≈⟨ ∙-congˡ (sym (identityˡ x)) ⟩
  (x ∙ y) ∙ (ε ∙ x) ≈⟨ identical y ε x ⟩
  x ∙ ((y ∙ ε) ∙ x) ≈⟨ ∙-congˡ (∙-congʳ (identityʳ y)) ⟩
  x ∙ (y ∙ x)       ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
z∙xzy≈zxz∙y : ∀ x y z → (z ∙ (x ∙ (z ∙ y))) ≈ (((z ∙ x) ∙ z) ∙ y)
z∙xzy≈zxz∙y x y z = sym (begin
  ((z ∙ x) ∙ z) ∙ y ≈⟨ (∙-congʳ (flex z x )) ⟩
  (z ∙ (x ∙ z)) ∙ y ≈⟨ sym (leftBol z x y) ⟩
  z ∙ (x ∙ (z ∙ y)) ∎)
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
x∙zyz≈xzy∙z : ∀ x y z → (x ∙ (z ∙ (y ∙ z))) ≈ (((x ∙ z) ∙ y) ∙ z)
x∙zyz≈xzy∙z x y z = begin
  x ∙ (z ∙ (y ∙ z))  ≈⟨ (∙-congˡ (sym (flex z y ))) ⟩
  x ∙ ((z ∙ y) ∙  z) ≈⟨ sym (rightBol z y x) ⟩
  ((x ∙ z) ∙ y) ∙ z  ∎
\end{minted}
\item
\begin{minted}[breaklines,samepage]{Agda}
z∙xyz≈zxy∙z : ∀ x y z → (z ∙ ((x ∙ y) ∙ z)) ≈ ((z ∙ (x ∙ y)) ∙ z)
z∙xyz≈zxy∙z x y z = sym (flex z (x ∙ y))
\end{minted}
\end{enumerate}