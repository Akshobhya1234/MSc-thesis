\chapter{Theory of Semigroup and Ring in Agda}
In early 20th century, mathematician Hilbert proposed the H\textsubscript{10} problem that argues if there exists a useful approach to verify whether a general Diophantine equation is solvable. \cite{larchey2020hilbert}. Although this problem was solved by 1970, In 1987 Siekmann and Szabo concluded that the unification problem of D\textsubscript{A}-rewriting system cannot be predicted. In \cite{deng2016characterizations} the author proposes a type (2,2,0) algebra that is a semigroup that is a general construct of D\textsubscript{A}-rewriting system. Other applications of semigroup in finite automata systems, probability theory and partial differential equations are explored in \cite{liaqat2021some}.\\

Ring is an algebraic structure has its applications in various branch if studies. Ring structures are studied in number theory \cite{noauthor_undated-eq}, quantum computing \cite{netto2008influence}, in cryptography \cite{ringcrypt} and many other fields. More variations of rings such as nearring, quasiring, ideal ring are being explored to make ring theory more dynamic, concrete and useable. The aim of this chapter is to define these structures and prove some of the most commonly used properties in the standard library that can help build other systems that uses these structures.
\section{Definition}
A set that has a binary operation is called Magma. Magma with associative property is called a semigroup. For binary operation \(∙\) on a set S, the associative property is defined as 
\begin{equation}
\forall x y z \in S : x ∙ (y ∙ z) = (x ∙ y) ∙ z.
\end{equation}
 A semigroup that satisfies commutative property is called commutative semigroup. For binary operation \( ∙ \) on a set S, commutative property is defined as 
\begin{equation}
\forall x y \in S : x ∙ y = y ∙ x
\end{equation}
The definition of associative and commutative property in Agda.
\begin{Verbatim}[commandchars=\\\{\},samepage=true]
	Associative : Op\textsubscript{2} A → Set _
	Associative _∙_ = ∀ x y z → ((x ∙ y) ∙ z) ≈ (x ∙ (y ∙ z))

	Commutative : Op\textsubscript{2} A → Set _
	Commutative _∙_ = ∀ x y → (x ∙ y) ≈ (y ∙ x)
\end{Verbatim}

The Semigroup structure can be structurally derived from Magma in Agda as 

\begin{Verbatim}[commandchars=\\\{\},samepage=true]
	record IsSemigroup (∙ : Op\textsubscript{2} A) : Set (a ⊔ ℓ) where
	  field
	    isMagma : IsMagma ∙
	    assoc   : Associative ∙
	
	  open IsMagma isMagma public
\end{Verbatim}
Similarly commutative semigroup can be derived from semigroup as
\begin{Verbatim}[commandchars=\\\{\},samepage=true]
	record IsCommutativeSemigroup (∙ : Op\textsubscript{2} A) : Set (a ⊔ ℓ) where
	  field
	    isSemigroup : IsSemigroup ∙
	    comm        : Commutative ∙
	
	  open IsSemigroup isSemigroup public
\end{Verbatim}
Semigroup and commutative semigroup structure definitions with direct product and morphism constructs were previously defined in agda standard library and hence will not be discussed in details in this chapter.\\
Non associative ring is an algebraic structure with two binary operations addition and multiplication. Addition is an abelian group and multiplication is unital magma and multiplication distributes over addition. A unital magma is a magma with identity element. 
\begin{Verbatim}
record IsNonAssociativeRing (+ * : Op₂ A) (-_ : Op₁ A) (0# 1# : A) : Set (a ⊔ ℓ) where
  field
    +-isAbelianGroup : IsAbelianGroup + 0# -_
    *-cong           : Congruent₂ *
    identity         : Identity 1# *
    distrib          : * DistributesOver +
    zero             : Zero 0# *

  open IsAbelianGroup +-isAbelianGroup public
\end{Verbatim}

A quasiring is a type (2,2) algebraic structure for which both addition and multiplication is a monoid and multiplication distributes over addition.

\begin{Verbatim}
record IsQuasiring (+ * : Op₂ A) (0# 1# : A) : Set (a ⊔ ℓ) where
  field
    +-isMonoid    : IsMonoid + 0#
    *-cong        : Congruent₂ *
    *-assoc       : Associative *
    *-identity    : Identity 1# *
    distrib       : * DistributesOver +
    zero          : Zero 0# *

  open IsMonoid +-isMonoid public
\end{Verbatim}

A quasiring with additive inverse is called a nearring. This implies that for nearring addition is a group and multiplication is a monoid and multiplication distributes over addition.

\begin{Verbatim}
record IsNearring (+ * : Op₂ A) (0# 1# : A) (_⁻¹ : Op₁ A) : Set (a ⊔ ℓ) where
  field
    isQuasiring : IsQuasiring + * 0# 1#
    +-inverse   : Inverse 0# _⁻¹ +
    ⁻¹-cong     : Congruent₁ _⁻¹

  open IsQuasiring isQuasiring public
\end{Verbatim}

Ring without one or rig or ring without unit is an algebraic structure with two binary operations such that addition is an abelian group and multiplication is a semigroup and multiplication distributes over addition Ring is rig with identity.
\begin{Verbatim}
record IsRingWithoutOne (+ * : Op₂ A) (-_ : Op₁ A) (0# : A) : Set (a ⊔ ℓ) where
  field
    +-isAbelianGroup : IsAbelianGroup + 0# -_
    *-cong           : Congruent₂ *
    *-assoc          : Associative *
    distrib          : * DistributesOver +
    zero             : Zero 0# *

  open IsAbelianGroup +-isAbelianGroup public
\end{Verbatim}
\section{Morphism} 
A structure preserving map between two structures is called morphism. In this section morphism of ring without one is given. Morphisms of other structures that are structurally different from ring without one discussed in this section can be found in agda standard library
\begin{Verbatim}
  record IsRingWithoutOneHomomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
    field
      +-isGroupHomomorphism : +.IsGroupHomomorphism ⟦_⟧
      *-homo : Homomorphic₂ ⟦_⟧ _*₁_ _*₂_

    open +.IsGroupHomomorphism +-isGroupHomomorphism public
      renaming (homo to +-homo; ε-homo to 0#-homo;
		 isMagmaHomomorphism to +-isMagmaHomomorphism)
\end{Verbatim} 
A Homomorphism that is injective is called monomorphism 
\begin{Verbatim}
  record IsRingWithoutOneMonomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
    field
      isRingWithoutOneHomomorphism : IsRingWithoutOneHomomorphism ⟦_⟧
      injective                    : Injective ⟦_⟧

    open IsRingWithoutOneHomomorphism isRingWithoutOneHomomorphism public
\end{Verbatim}
A monomorphism that is bijective is called an isomorphism
\begin{Verbatim}
  record IsRingWithoutOneMonomorphism (⟦_⟧ : A → B) : Set (a ⊔ ℓ₁ ⊔ ℓ₂) where
    field
      isRingWithoutOneHomomorphism : IsRingWithoutOneHomomorphism ⟦_⟧
      injective                    : Injective ⟦_⟧

    open IsRingWithoutOneHomomorphism isRingWithoutOneHomomorphism public
\end{Verbatim}
\section{Morphism composition}
If f is a morphism such that f : a \(\rightarrow\) b and g is a morphism on same structure such that g : b \(\rightarrow\) c then composition of morphism can be defined as g ∘ f : a \(\rightarrow\) c.
\begin{Verbatim}
  isRingWithoutOneHomomorphism
    : IsRingWithoutOneHomomorphism R₁ R₂ f
    → IsRingWithoutOneHomomorphism R₂ R₃ g
    → IsRingWithoutOneHomomorphism R₁ R₃ (g ∘ f)
  isRingWithoutOneHomomorphism f-homo g-homo = record
    { +-isGroupHomomorphism = isGroupHomomorphism ≈₃-trans
		 F.+-isGroupHomomorphism G.+-isGroupHomomorphism
    ; *-homo                 = λ x y → ≈₃-trans 
		(G.⟦⟧-cong (F.*-homo x y)) (G.*-homo (f x) (f y))
    } where module F = IsRingWithoutOneHomomorphism f-homo;
		 module G = IsRingWithoutOneHomomorphism g-homo
\end{Verbatim}

\section{Direct Product}
The direct product M \(\times\) N of two ring without one structures M and N is defined as a pair (m,n) where m \(\in\) M and n \(\in\) N.
\begin{Verbatim}
ringWithoutOne : RingWithoutOne a ℓ₁ → 
		RingWithoutOne b ℓ₂ → RingWithoutOne (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
ringWithoutOne R S = record
  { isRingWithoutOne = record
      { +-isAbelianGroup = AbelianGroup.isAbelianGroup
		 ((abelianGroup R.+-abelianGroup S.+-abelianGroup))
      ; *-cong           = Semigroup.∙-cong
		 (semigroup R.*-semigroup S.*-semigroup)
      ; *-assoc   = Semigroup.assoc (semigroup R.*-semigroup S.*-semigroup)
      ; distrib     = (λ x y z → 
		(R.distribˡ , S.distribˡ) <*> x <*> y <*> z)
                            , (λ x y z → 
		(R.distribʳ , S.distribʳ) <*> x <*> y <*> z)
      ; zero     = uncurry (λ x y → R.zeroˡ x , S.zeroˡ y)
                            , uncurry (λ x y → R.zeroʳ x , S.zeroʳ y)
      }

  } where module R = RingWithoutOne R; module S = RingWithoutOne S
\end{Verbatim}
The direct product M \(\times\) N of two non associative ring a structures M and N is defined as a pair (m,n) where m \(\in\) M and n \(\in\) N.
\begin{Verbatim}
nonAssociativeRing : NonAssociativeRing a ℓ₁ →
		 NonAssociativeRing b ℓ₂ → NonAssociativeRing (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
nonAssociativeRing R S = record
  { isNonAssociativeRing = record
      { +-isAbelianGroup = AbelianGroup.isAbelianGroup 
		((abelianGroup R.+-abelianGroup S.+-abelianGroup))
      ; *-cong           = UnitalMagma.∙-cong 
		(unitalMagma R.*-unitalMagma S.*-unitalMagma)
      ; *-identity       = UnitalMagma.identity 
		(unitalMagma R.*-unitalMagma S.*-unitalMagma)
      ; distrib          = (λ x y z → 
		(R.distribˡ , S.distribˡ) <*> x <*> y <*> z)
                            , (λ x y z → 
		(R.distribʳ , S.distribʳ) <*> x <*> y <*> z)
      ; zero             = uncurry (λ x y → R.zeroˡ x , S.zeroˡ y)
                            , uncurry (λ x y → R.zeroʳ x , S.zeroʳ y)
      }

  } where module R = NonAssociativeRing R; module S = NonAssociativeRing S
\end{Verbatim}
The direct product M \(\times\) N of two quasiring a structures M and N is defined as a pair (m,n) where m \(\in\) M and n \(\in\) N.
\begin{Verbatim}
quasiring : Quasiring a ℓ₁ →
		 Quasiring b ℓ₂ → Quasiring (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
quasiring R S = record
  { isQuasiring = record
      { +-isMonoid = Monoid.isMonoid
		 ((monoid R.+-monoid S.+-monoid))
      ; *-cong           = Monoid.∙-cong
		 (monoid R.*-monoid S.*-monoid)
      ; *-assoc          = Monoid.assoc
		 (monoid R.*-monoid S.*-monoid)
      ; *-identity       = Monoid.identity
		 ((monoid R.*-monoid S.*-monoid))
      ; distrib          = (λ x y z →
		 (R.distribˡ , S.distribˡ) <*> x <*> y <*> z)
                            , (λ x y z →
		 (R.distribʳ , S.distribʳ) <*> x <*> y <*> z)
      ; zero             = uncurry (λ x y → R.zeroˡ x , S.zeroˡ y)
                            , uncurry (λ x y → R.zeroʳ x , S.zeroʳ y)
      }

  } where module R = Quasiring R; module S = Quasiring S
\end{Verbatim}
The direct product M \(\times\) N of two nearring a structures M and N is defined as a pair (m,n) where m \(\in\) M and n \(\in\) N.
\begin{Verbatim}
nearring : Nearring a ℓ₁ → 
		Nearring b ℓ₂ → Nearring (a ⊔ b) (ℓ₁ ⊔ ℓ₂)
nearring R S =  record
  { isNearring = record
      { isQuasiring = Quasiring.isQuasiring
		 (quasiring R.quasiring S.quasiring)
      ; +-inverse   = (λ x → (R.+-inverseˡ , S.+-inverseˡ) <*> x)
                      , (λ x → (R.+-inverseʳ , S.+-inverseʳ) <*> x)
      ; ⁻¹-cong     = map R.⁻¹-cong S.⁻¹-cong
      }
  } where module R = Nearring R; module S = Nearring S
\end{Verbatim}
\section{Properties}

This provide proof that was contributed by the author and other proofs can be found in agda standard library.
\subsection{Properties of Semigroup}
Let (S, ∙) be a semigroup then\\
a) S is alternative. The Semigroup S left alternative if \( \forall x,y \in S : (x ∙ x) ∙ y = x ∙ (x ∙ y) \) and right alternative is \(\forall x,y \in S : x ∙ (y ∙ y) = (x ∙ y) ∙ y\). Semigroup is said to be alternative if it is both left and right alternative. \\
b) S is flexible. The Semigroup S is flexible if \(\forall x y \in S: x ∙ (y ∙ x) = (x ∙ y) ∙ x\)\\
c) S has Jordan identity.  Jordan identity for binary operation ∙ can be defined on set S as \( \forall x y z \in S: (x ∙ y) ∙ (x ∙ x) = x ∙ (y ∙ (x ∙ x)) \)\\

Proof:
\begin{Verbatim}[commandchars=\\\{\},samepage=true]
a)
alternative\textsuperscript{l} : LeftAlternative _∙_
alternative\textsuperscript{l} x y = assoc x x y

alternative\textsuperscript{r} : RightAlternative _∙_
alternative\textsuperscript{r} x y = sym (assoc x y y)

alternative : Alternative _∙_
alternative = alternative\textsuperscript{l} , alternative\textsuperscript{r}
\end{Verbatim}
 
\begin{Verbatim}[commandchars=\\\{\},samepage=true]
b)
flexible : Flexible _∙_
flexible x y = assoc x y x
\end{Verbatim}

\begin{Verbatim}[commandchars=\\\{\},samepage=true]
c)
xy∙xx≈x∙yxx : ∀ x y → (x ∙ y) ∙ (x ∙ x) ≈ x ∙ (y ∙ (x ∙ x))
xy∙xx≈x∙yxx x y = assoc x y ((x ∙ x))
\end{Verbatim}
\subsection{Properties of Commutative Semigroup}
Let (S, ∙) be a commutative semigroup then\\
a) S is semimedial. The commutative semigroup S is left semimedial if  \( \forall x y z \in S: (x ∙ x) ∙ (y ∙ z) = (x ∙ y) ∙ (x ∙ z) \) and right semimedial if \( \forall x y z \in S: (y ∙ z) ∙ (x ∙ x) = (y ∙ x) ∙ (z ∙ x) \). A structure is semimedial if it is both left and right semimedial. \\
b) S is middle semimedia.  The commutative semigroup S is middle semimedial if  \(\forall x y z  \in S: (x ∙ y) ∙ (z ∙ x) = (x ∙ z) ∙ (y ∙ x)\)\\
\begin{Verbatim}[commandchars=\\\{\},samepage=true]
a)
semimedial\textsuperscript{l} : LeftSemimedial _∙_
semimedial\textsuperscript{l} x y z = begin
  (x ∙ x) ∙ (y ∙ z) ≈⟨ assoc x x (y ∙ z) ⟩
  x ∙ (x ∙ (y ∙ z)) ≈⟨ ∙-cong\textsuperscript{l} (sym (assoc x y z)) ⟩
  x ∙ ((x ∙ y) ∙ z) ≈⟨ ∙-cong\textsuperscript{l} (∙-cong\textsuperscript{r} (comm x y)) ⟩
  x ∙ ((y ∙ x) ∙ z) ≈⟨ ∙-cong\textsuperscript{l} (assoc y x z) ⟩
  x ∙ (y ∙ (x ∙ z)) ≈⟨ sym (assoc x y ((x ∙ z))) ⟩
  (x ∙ y) ∙ (x ∙ z) ∎

semimedial\textsuperscript{r} : RightSemimedial _∙_
semimedial\textsuperscript{r} x y z = begin
  (y ∙ z) ∙ (x ∙ x) ≈⟨ assoc y z (x ∙ x) ⟩
  y ∙ (z ∙ (x ∙ x)) ≈⟨ ∙-cong\textsuperscript{l} (sym (assoc z x x)) ⟩
  y ∙ ((z ∙ x) ∙ x) ≈⟨ ∙-cong\textsuperscript{l} (∙-cong\textsuperscript{r} (comm z x)) ⟩
  y ∙ ((x ∙ z) ∙ x) ≈⟨ ∙-cong\textsuperscript{l} (assoc x z x) ⟩
  y ∙ (x ∙ (z ∙ x)) ≈⟨ sym (assoc y x ((z ∙ x))) ⟩
  (y ∙ x) ∙ (z ∙ x) ∎

semimedial : Semimedial _∙_
semimedial = semimedial\textsuperscript{l} , semimedial\textsuperscript{r}
\end{Verbatim}

\begin{Verbatim}[commandchars=\\\{\},samepage=true]
b)
middleSemimedial : ∀ x y z → (x ∙ y) ∙ (z ∙ x) ≈ (x ∙ z) ∙ (y ∙ x)
middleSemimedial x y z = begin
  (x ∙ y) ∙ (z ∙ x) ≈⟨ assoc x y ((z ∙ x)) ⟩
  x ∙ (y ∙ (z ∙ x)) ≈⟨ ∙-cong\textsuperscript{l} (sym (assoc y z x)) ⟩
  x ∙ ((y ∙ z) ∙ x) ≈⟨ ∙-cong\textsuperscript{l} (∙-cong\textsuperscript{r} (comm y z)) ⟩
  x ∙ ((z ∙ y) ∙ x) ≈⟨ ∙-cong\textsuperscript{l} ( assoc z y x) ⟩
  x ∙ (z ∙ (y ∙ x)) ≈⟨ sym (assoc x z ((y ∙ x))) ⟩
  (x ∙ z) ∙ (y ∙ x) ∎
\end{Verbatim}

\subsection{Properties of Ring without one}

Let (R, +, *, -, 0) be ring without one structure then
a) \(\forall x, y \in R\): - (x * y) = - x * y\\
b) \(\forall x, y \in R\): - (x * y) = x * - y\\
proof:
\begin{Verbatim}
a)
-‿distribˡ-* : ∀ x y → - (x * y) ≈ - x * y
-‿distribˡ-* x y = sym $ begin
  - x * y                        ≈⟨ sym $ +-identityʳ (- x * y) ⟩
  - x * y + 0#                   ≈⟨ +-congˡ $ sym ( -‿inverseʳ (x * y) ) ⟩
  - x * y + (x * y + - (x * y))  ≈⟨ sym $ +-assoc (- x * y) (x * y) (- (x * y)) ⟩
  - x * y + x * y + - (x * y)    ≈⟨ +-congʳ $ sym ( distribʳ y (- x) x ) ⟩
  (- x + x) * y + - (x * y)      ≈⟨ +-congʳ $ *-congʳ $ -‿inverseˡ x ⟩
  0# * y + - (x * y)             ≈⟨ +-congʳ $ zeroˡ y ⟩
  0# + - (x * y)                 ≈⟨ +-identityˡ (- (x * y)) ⟩
  - (x * y)                      ∎
\end{Verbatim}
\begin{Verbatim}
b)
-‿distribʳ-* : ∀ x y → - (x * y) ≈ x * - y
-‿distribʳ-* x y = sym $ begin
  x * - y                        ≈⟨ sym $ +-identityˡ (x * (- y)) ⟩
  0# + x * - y                   ≈⟨ +-congʳ $ sym ( -‿inverseˡ (x * y) ) ⟩
  - (x * y) + x * y + x * - y    ≈⟨ +-assoc (- (x * y)) (x * y) (x * (- y)) ⟩
  - (x * y) + (x * y + x * - y)  ≈⟨ +-congˡ $ sym ( distribˡ x y ( - y) )  ⟩
  - (x * y) + x * (y + - y)      ≈⟨ +-congˡ $ *-congˡ $ -‿inverseʳ y ⟩
  - (x * y) + x * 0#             ≈⟨ +-congˡ $ zeroʳ x ⟩
  - (x * y) + 0#                 ≈⟨ +-identityʳ (- (x * y)) ⟩
  - (x * y)                      ∎
\end{Verbatim}
\subsection{Properties of Ring}
Let (R, +, *, -, 0, 1) be a ring structure then
a) \(\forall x \in R\): - 1 * x = -x\\
b) \(\forall x \in R\): if x + x = 0 then x = 0\\
c) \(\forall x,y,z \in R\): x * (y - z) = x * y - x * z\\
d) \(\forall x,y,z \in R\): (y - z) * x = (y * x) - (z * x)\\
Proof
\begin{Verbatim}
a)
-1*x≈-x : ∀ x → - 1# * x ≈ - x
-1*x≈-x x = begin
  - 1# * x    ≈⟨ sym (-‿distribˡ-* 1# x ) ⟩
  - (1# * x)  ≈⟨ -‿cong ( *-identityˡ x ) ⟩
  - x         ∎
\end{Verbatim}
\begin{Verbatim}
b)
x+x≈x⇒x≈0 : ∀ x → x + x ≈ x → x ≈ 0#
x+x≈x⇒x≈0 x eq = begin
  x ≈⟨ sym(+-identityʳ x) ⟩
  x + 0# ≈⟨ +-congˡ (sym (-‿inverseʳ x)) ⟩
  x + (x - x) ≈⟨ sym (+-assoc x x (- x)) ⟩
  x + x - x ≈⟨ +-congʳ(eq) ⟩
  x - x ≈⟨ -‿inverseʳ x ⟩
  0# ∎
\end{Verbatim}
\begin{Verbatim}
c)
x[y-z]≈xy-xz : ∀ x y z → x * (y - z) ≈ x * y - x * z
x[y-z]≈xy-xz x y z = begin
  x * (y - z)      ≈⟨ distribˡ x y (- z) ⟩
  x * y + x * - z  ≈⟨ +-congˡ (sym (-‿distribʳ-* x z)) ⟩
  x * y - x * z    ∎
\end{Verbatim}
\begin{Verbatim}
d)
[y-z]x≈yx-zx : ∀ x y z → (y - z) * x ≈ (y * x) - (z * x)
[y-z]x≈yx-zx x y z = begin
  (y - z) * x      ≈⟨ distribʳ x y (- z) ⟩
  y * x + - z * x  ≈⟨ +-congˡ (sym (-‿distribˡ-* z x)) ⟩
  y * x - z * x    ∎
\end{Verbatim}